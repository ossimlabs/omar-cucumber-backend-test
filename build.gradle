import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.DockerTagImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

plugins {
    // Apply the groovy plugin to add support for Groovy
    id 'groovy'
    // Apply the application plugin to add support for building an application
    id 'application'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
    id 'java'
    id 'com.bmuschko.docker-remote-api' version '4.6.2'
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

if(project.hasProperty("runTags"))
{
    tags = Eval.me runTags
}

repositories {
    maven { url ossimMavenProxy }
}

dependencies {
    // Use the latest Groovy version for building this library
    implementation 'org.codehaus.groovy:groovy-all:2.5.4'
    compile group: 'com.github.mkolisnyk', name: 'cucumber-runner', version: '1.3.5'
    compile group: 'io.cucumber', name: 'cucumber-groovy', version: '4.3.0'
    testCompile group: 'io.cucumber', name: 'cucumber-junit', version: '4.3.1'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.5.0-M1'
    compile "com.amazonaws:aws-java-sdk-sqs:1.11.26"
    compile "org.gebish:geb-core:1.0"
    // Tomcat libraries
    compile "org.apache.tomcat.embed:tomcat-embed-core:8.5.23"
    compile "org.apache.tomcat.embed:tomcat-embed-jasper:8.5.23"
    compile "org.apache.tomcat:tomcat-jasper:8.5.23"
    compile "org.apache.tomcat:tomcat-jasper-el:8.5.23"
    compile "org.apache.tomcat:tomcat-jsp-api:8.5.23"
    compile("org.geoscript:geoscript-groovy:1.7.0"){
        exclude module: "ehcache"
        exclude module: "slf4j-log4j12"
        exclude module: "xml-apis"
        exclude module: "commons-beanutils"
        exclude module: "gt-jdbc-spatialite"
    }

    cucumberRuntime files( "${jar.archivePath}" )
}

// Define the main class for the application
mainClassName = 'omar.cucumber.backend.test.Application'
defaultTasks = ['shadowJar']
version = "${buildVersion}"

shadowJar {
    manifest {
        attributes 'Main-Class': 'omar.cucumber.backend.test.App'
    }
}

run {
    if (multi == "true")
    {
        environment 'multi', 'true'
    }
    if (runEnvironment == "local")
    {
        environment 'runEnv', 'local'
    } else {
        environment 'runEnv', ''
    }
}

docker {
    registryCredentials {
        username.set("${dockerRegistryUsername}".toString())
        password.set("${dockerRegistryPassword}".toString())
        url.set("${dockerRegistryUrl}".toString())
    }
}

task createDockerfile(type: Dockerfile) {
    dependsOn shadowJar
    from "${dockerBaseImage}:${dockerBaseImageTag}"
    label(['maintainer': 'MAXAR'])
    runCommand('yum -y update')
    runCommand('yum -y install java-1.8.0-openjdk')
    environmentVariable("CURL_USER_NAME", "${cUname}")
    environmentVariable("CURL_PASSWORD", "${cPword}")
    runCommand('useradd -u 1001 -r -g 0 --create-home -d /home/omar -s /sbin/nologin -c \'Default Application User\' omar')
    environmentVariable("HOME","/home/omar")
    runCommand('mkdir /home/omar/build')
    runCommand('mkdir /home/omar/build/classes')
    runCommand('mkdir /home/omar/src')
    runCommand('mkdir /home/omar/src/main')
    copyFile('main/', '/home/omar/src/main')
    copyFile("groovy", '/home/omar/build/classes')
    copyFile("omar-cucumber-backend-test-${version}.jar", '/home/omar/')
    runCommand('chown 1001:0 -R /home/omar')
    runCommand('chmod -R 777 /home/omar')
    exposePort(8080)
    user('1001')
    volume('/conf')
    workingDir('/home/omar')
    defaultCommand('java', '-server', '-Xms256m', '-Xmx1024m', '-Djava.awt.headless=true', '-XX:+CMSClassUnloadingEnabled', '-XX:+UseGCOverheadLimit', '-Djava.security.egd=file:/dev/./urandom', '-jar', "omar-cucumber-backend-test-${version}.jar")
}

task copyFile(type: Copy) {
    dependsOn createDockerfile
    from 'src'
    from 'build/classes'
    from "build/libs/omar-cucumber-backend-test-${version}.jar"
    into 'build/docker'
}

task buildDockerImage(type: DockerBuildImage) {
    dependsOn copyFile
    tags.add("${project.name}:${buildVersion}")
}

task tagDockerImage(type: DockerTagImage) {
    dependsOn buildDockerImage
    targetImageId "${project.name}:${buildVersion}"
    tag.set("${buildVersion}".toString())
    repository.set("${dockerRegistryUrl}/${project.name}".toString())
}

task pushDockerImage(type: DockerPushImage) {
    dependsOn tagDockerImage
    registryCredentials = registryCredentials
    imageName.set("${dockerRegistryUrl}/${project.name}".toString())
    tag.set("${buildVersion}".toString())
}

task backend() {
    dependsOn assemble
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime
            if(System.env?.JVM_ARGS){
                println System.env?.JVM_ARGS
                jvmArgs = System.env.JVM_ARGS.split(" ").collect(){it.toString()}
            }
            args = [
                    "--tags",
                    "@image_space, @mensa_service, @wfs_image_search, @wms_ortho, @web_coverage_service, @map_tile_service, @omar_geoscript, @o2_basemap, @download_service, @superoverlay_service",
                    "--tags",
                    "~@C2S",
                    '--plugin', 'json:build/backend.json',
                    '--glue', 'src/main',
                    'src/cucumber/resources']

        }
    }
}

//pretty output.
build.dependsOn backend